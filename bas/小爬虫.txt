0 GRAPH:BOX 31,30,129,49,0,2:BOX 31,30,128,48:DRAW 129,49:LOCATE 3,5:PRINT "小爬虫满街爬
10 FOR A=1  TO 2500:NEXT
30 A=PEEK(200):FOR B=0  TO A:C=RND(1):NEXT:CLS:DIM A(7,3,3)
35 BOX 5,0,156,79,0,2:BOX 5,0,155,78:DRAW 156,79:FOR A=1  TO 4
40 BOX 5,A*18-10,155,A*18-2:NEXT:FOR A=1  TO 8:BOX A*18-5,0,A*18+3,78:NEXT:GOTO 200
45 B=((Q+F*A)-12)/18:IF B*(B-3)=4 THEN B=2.4-B*.6
46 A=((P+E*A)-17)/18:IF A*(A-7)=8 THEN A=INT(6.5-A*.8)
47 RETURN
50 M=A*18+10:N=B*18+5:BOX M,N,M+14,N+14,1,1-V:BOX M+1,N+1,M+13,N+13,0,V:C=0
60 IF A(A,B,0)=1 THEN BOX M+4,N,M+10,N+10,1,V:C=C+1
70 IF A(A,B,1)=1 THEN BOX M+4,N+4,M+14,N+10,1,V:C=C+1
80 IF A(A,B,2)=1 THEN BOX M+4,N+4,M+10,N+14,1,V:C=C+1
90 IF A(A,B,3)=1 THEN BOX M,N+4,M+10,N+10,1,V:C=C+1
100 IF C=1 THEN BOX M+6,N+6,M+8,N+8,0,1-V:B(A,B)=2
110 IF C=0 THEN B(A,B)=1:47 ELSE 47 
120 ON (2-E)*(F=0)+(F+1)*(E=0)GOTO 150,170,190:BOX P-2,Q-3,P+2,Q+2,0,2:DRAW P-1,Q-1,2
130 DRAW P,Q+1,2:DRAW P+1,Q-1,2:BOX P-1,Q-2,P+1,Q+1,1,2:LINE P-1,Q-5,P,Q-4,2
140 DRAW P+1,Q-5,2:RETURN
150 BOX P-2,Q-2,P+3,Q+2,0,2:DRAW P+1,Q-1,2:DRAW P-1,Q,2:DRAW P+1,Q+1,2
160 BOX P-1,Q-1,P+2,Q+1,1,2:LINE P+5,Q-1,P+4,Q,2:DRAW P+5,Q+1,2:RETURN
170 BOX P-2,Q-2,P+2,Q+3,0,2:DRAW P+1,Q+1,2:DRAW P,Q-1,2:DRAW P-1,Q+1,2
180 BOX P-1,Q-1,P+1,Q+2,1,2:LINE P+1,Q+5,P,Q+4,2:DRAW P-1,Q+5,2:RETURN
190 BOX P-3,Q-2,P+2,Q+2,0,2:DRAW P-1,Q+1,2:DRAW P+1,Q,2:DRAW P-1,Q-1,2
195 BOX P-2,Q-1,P+1,Q+1,1,2:LINE P-5,Q+1,P-4,Q,2:DRAW P-5,Q-1,2:RETURN
197 BOX G*18+8,H*18+3,G*18+26,H*18+21,1,2:RETURN
200 FOR B=0  TO 3:FOR A=0  TO 7:C%=RND(1)*4:C=0:D=0:E=0:F=0:ON C% GOTO 220,240,240
210 IF RND(1)>.5 THEN D=1:F=1:290 ELSE C=1:E=1:290
220 IF RND(1)>.5 THEN C=1 ELSE E=1
230 IF RND(1)>.5 THEN D=1:290 ELSE F=1:290
240 ON RND(1)*4 GOTO 250,260,270:C=1:GOTO 280
250 D=1:GOTO 280
260 E=1:GOTO 280
270 F=1
280 IF C%=3 THEN C=1-C:D=1-D:E=1-E:F=1-F
290 A(A,B,0)=C:A(A,B,1)=D:A(A,B,2)=E:A(A,B,3)=F:GOSUB 50:NEXT:NEXT
300 A%=RND(1)*8:B%=RND(1)*4:C=INT(RND(1)*4):IF A(A%,B%,C)=0 THEN 300
310 P=A%*18+17:Q=B%*18+12:E=(2-C)*(C/2>INT(C/2)):F=(C-1)*(C/2=INT(C/2)):GOSUB 120
320 G=A%+E:IF G*(G-7)=8 THEN G=INT(-G*.8+6.5)
330 H=B%+F:IF H*(H-3)=4 THEN H=-H*.6+2.4
340 GOSUB 197:B(A%,B%)=1:QA1=A%:QB1=B%:A$=INKEY$
350 FOR Z=1  TO 15:IF PEEK(196)=247 THEN IF O1=0 THEN I=0:J=-1:O1=1:410 ELSE 430 ELSE O1=0
360 IF PEEK(195)=247 THEN IF O2=0 THEN I=0:J=1:O2=1:410 ELSE 430 ELSE O2=0
370 IF PEEK(193)=247 THEN IF O3=0 AND B(G,H)<>1 THEN O3=1:420 ELSE 430 ELSE O3=0
380 K=PEEK(191):IF K=127 THEN IF O4=0 THEN I=-1:J=0:O4=1:410 ELSE 430 ELSE O4=0
390 IF K=247 THEN IF O5=0 THEN I=1:J=0:O5=1:410 ELSE 430 ELSE O5=0
400 GOTO 430
410 GOSUB 197:G=G+I:IF G*(G-7)=8 THEN G=INT(-G*.8+6.5)
415 H=H+J:IF H*(H-3)=4 THEN H=-H*.6+2.4
417 GOSUB 197:GOTO 430
420 SWAP A(G,H,3),A(G,H,2):SWAP A(G,H,2),A(G,H,1):SWAP A(G,H,1),A(G,H,0):A=G:B=H:V=1
425 GOSUB 50
430 sleep 30:NEXT:ON S-INT(S/6)*6+1 GOTO 620,450,630,620,470,520
450 A=15:GOSUB 45:QA=QA1:QB=QB1:QA1=A:QB1=B:B2=B(A,B):B(A,B)=1
460 IF A(A,B,(E+2)*(F=0)+(1-F)*(E=0))=0 OR B2=1 THEN 670 ELSE 620 
470 A=QA:B=QB:GOSUB 475:GOTO 620
475 W=W+1:V=0:IF G=A AND H=B THEN V=1
490 FOR O=0  TO 3:A(A,B,O)=0:NEXT:IF RND(1)>.9 THEN 500
495 FOR O=0  TO 2:A(A,B,RND(1)*4)=1:NEXT
500 B(A,B)=0:GOSUB 50
510 RETURN
520 A=3:GOSUB 45:R=(2-E)*(F=0)+(F+1)*(E=0):T=(R/2>INT(R/2))*2+2-R:U=(R+T)/2:V=6-U*3
530 IF B2<2 THEN 590
540 U=0:FOR BB=0  TO 3:FOR AA=0  TO 7:IF B(AA,BB)<2 OR A(AA,BB,T)=0 THEN 560
550 U=1:GOSUB 475:A=AA:B=BB:B(A,B)=1:AA=7:BB=3:R=T:QA1=A:QB1=B
560 NEXT:NEXT:IF U=0 THEN 670 
570 P=A*18+17:Q=B*18+12:E=(2-R)*(R/2>INT(R/2)):F=(R-1)*(R/2=INT(R/2)):GOSUB 120:S=S+1
580 GOTO 350
590 IF A(A,B,R)=1 THEN 610
600 IF RND(1)>.5 THEN R=U ELSE R=V
605 IF A(A,B,R)=0 THEN 600
610 GOSUB 120:GOTO 570
620 GOSUB 120
625 P=P+E*3:Q=Q+F*3:S=S+1:GOSUB 120:GOTO 350
630 GOSUB 120:IF P*(160-P)=1639 THEN P=INT(163.5-P*1.04):660
640 IF Q*(78-Q)=432 THEN Q=INT(81.5-Q*1.08):660
650 GOTO 625
660 GOSUB 120:S=S+1:GOTO 350
670 BEEP:GOSUB 197:BOX 31,30,129,48:BOX 32,31,130,49:BOX 32,31,128,47,0,0:LOCATE 3,5
680 PRINT "你的得分:00":LOCATE 3,17-LEN(STR$(W)):PRINT W
690 POKE 199,141:INKEY$:INKEY$:CLEAR:GOTO 30