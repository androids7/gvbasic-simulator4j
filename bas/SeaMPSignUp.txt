10 GRAPH
20 LOCATE 2,3:PRINT "海空大战注册程序":LOCATE 3,3:PRINT "For Multi-Player"
30 LOCATE 4,5:PRINT "Version 1.00"
40 K$=INKEY$
50 CLEAR
55 GRAPH
60 PRINT "      新兵报到","主    武器工厂","菜    数据中心","单    版本信息",
70 PRINT "      退出程序";
80 CHO=1:CHE=0
90 IF CHE<>0 THEN BOX 48,(CHE-1)*16,111,CHE*16-1,1,2
100 BOX 48,(CHO-1)*16,111,CHO*16-1,1,2
110 K=ASC(INKEY$)
120 IF K=20 AND CHO=1 THEN CHO=5:CHE=1:GOTO 90
130 IF K=20 AND CHO>1 THEN CHE=CHO:CHO=CHO-1:GOTO 90
140 IF K=21 AND CHO=5 THEN CHO=1:CHE=5:GOTO 90
150 IF K=21 AND CHO<5 THEN CHE=CHO:CHO=CHO+1:GOTO 90
160 IF K=27 THEN POKE 199,155
170 IF K=13 THEN 190
180 BEEP:GOTO 110
190 ON CHO GOTO 240,470,1100,210,200
200 POKE 199,155
210 GRAPH
220 PRINT "版本：v1.00","设计：Lyster.S.K","编写：Lyster.S.K","QQ：27138435","<跳出>键返回";
230 K=ASC(INKEY$):IF K=27 THEN 50 ELSE BEEP:GOTO 230
240 TEXT
250 PRINT "请输入新用户名（英文，不超过8 字符）：":INPUT "] ";NAME$
260 IF LEN(NAME$)=0 THEN 50
270 IF LEN(NAME$)>8 THEN 240
280 OPEN "SD"+NAME$ FOR RANDOM AS #1
290 IF LOF(1)<>0 THEN 300 ELSE 320
300 CLOSE #1:GRAPH:PRINT "抱歉，此用户名已注册，请更换。","<跳出>键返回"
310 K=ASC(INKEY$):IF K=27 THEN 240 ELSE BEEP:GOTO 310
320 CLOSE #1
330 TEXT
340 PRINT "请设置密码：":INPUT "] ";PAS$(1)
350 IF LEN(PAS$(1))=0 THEN BEEP:GOTO 330
360 PRINT "请确认密码：":INPUT "] ";PAS$(2)
370 IF PAS$(2)=PAS$(1) THEN 400 ELSE 380
380 GRAPH:PRINT "两次输入不一致，请重试。":K$=INKEY$:GOTO 330
400 GRAPH:LOCATE 3,3:PRINT "正在提交申请 ..."
410 OPEN "SD"+NAME$ FOR OUTPUT AS #1:M=12000:R=1
420 WRITE #1,NAME$,PAS$(1),M,S,R
430 FOR I=1  TO 10:WRITE #1,Z:NEXT I
440 CLOSE #1
450 GRAPH:PRINT "新兵";NAME$;"：","注册成功！"," "," ","任意键返回主菜单";:K$=INKEY$
460 GOTO 50
470 TEXT
480 PRINT "身份验证：","请输入用户名：":INPUT "] ";NAME$
490 IF LEN(NAME$)=0 THEN 50 
500 OPEN "SD"+NAME$ FOR RANDOM AS #1 
510 IF LOF(1)=0 THEN 520 ELSE 540
520 CLOSE #1:GRAPH
530 PRINT "这个用户名还未注册，请重新输入！":K$=INKEY$:TEXT:GOTO 470
540 CLOSE #1
550 OPEN "SD"+NAME$ FOR INPUT AS #1:INPUT #1,NAME$,PASS$:CLOSE #1
560 PRINT "请输入密码：":INPUT "] ";PAS$
570 IF PASS$=PAS$ THEN 590 ELSE 580
580 GRAPH:PRINT "密码错误！":K$=INKEY$:TEXT:GOTO 560
590 GRAPH
600 OPEN "SD"+NAME$ FOR INPUT AS #1
610 INPUT #1,NAME$,PASS$,MONEY,SC,RANK
620 FOR X=1  TO 3
630 FOR Y=1  TO 3
640 INPUT #1,WEA(X,Y)
650 NEXT Y
660 NEXT X
670 CLOSE #1
680 DIM SEL$(4,3),PR(3,3)
690 SEL$(4,1)="舰载攻击机":SEL$(4,2)="导弹类兵器":SEL$(4,3)="舰船升级系统"
700 SEL$(1,1)="战斗轰炸机":SEL$(1,2)="侦察机":SEL$(1,3)="超音速侦察机"
710 SEL$(2,1)="重型反舰导弹":SEL$(2,2)="多弹头导弹":SEL$(2,3)="高级多弹头导弹"
720 SEL$(3,1)="舰载机导航系统":SEL$(3,2)="宙斯盾系统":SEL$(3,3)="精确制导系统"
730 PR(1,1)=3600:PR(1,2)=5400:PR(1,3)=7200
740 PR(2,1)=3600:PR(2,2)=1500:PR(2,3)=2000
750 PR(3,1)=2400:PR(3,2)=10000:PR(3,3)=15000
760 GRAPH:PRINT "请选择要购买的武器：":MENU=4
765 LOCATE 5,1:PRINT "<跳出>键退出";:CLEAN$="                    "
770 CHO=1:CHE=0
780 FOR I=2  TO 4
790 LOCATE I,1:PRINT CLEAN$:LOCATE I,1:PRINT SEL$(MENU,I-1)
800 NEXT I
810 IF CHE<>0 THEN 820 ELSE 830
820 BOX 1,CHE*16,160,CHE*16+15,1,2
830 BOX 1,CHO*16,160,CHO*16+15,1,2
840 K=ASC(INKEY$)
850 IF K=20 AND CHO>1 THEN CHE=CHO:CHO=CHO-1:GOTO 810
860 IF K=21 AND CHO<3 THEN CHE=CHO:CHO=CHO+1:GOTO 810
870 IF K=27 AND MENU=4 THEN 1000
875 IF K=27 AND MENU<>4 THEN MENU=4:GOTO 770
880 IF K=13 THEN 900
890 BEEP:GOTO 840
900 IF MENU=4 THEN MENU=CHO:GOTO 770
910 GRAPH:A=MENU:B=CHO
920 PRINT SEL$(A,B);" $";PR(A,B)
930 LOCATE 4,1:PRINT "<B>键购买  <S>键卖出","<跳出>键返回";
940 LOCATE 2,1:PRINT CLEAN$:LOCATE 3,1:PRINT CLEAN$
945 LOCATE 2,1:PRINT "现有数量：";WEA(A,B),"现有资金：";MONEY
950 K$=INKEY$
960 IF K$="b" AND MONEY>=PR(A,B) THEN WEA(A,B)=WEA(A,B)+1:MONEY=MONEY-PR(A,B):GOTO 940
970 IF K$="s" AND WEA(A,B)>=1 THEN WEA(A,B)=WEA(A,B)-1:MONEY=MONEY+PR(A,B):GOTO 940
980 IF ASC(K$)=27 THEN 760
990 BEEP:GOTO 950
1000 GRAPH
1010 OPEN "SD"+NAME$ FOR OUTPUT AS #1
1020 WRITE #1,NAME$,PASS$,MONEY,SC,RANK
1030 FOR X=1  TO 3
1040 FOR Y=1  TO 3
1050 WRITE #1,WEA(X,Y)
1060 NEXT Y
1070 NEXT X
1080 CLOSE #1
1090 GOTO 50
1100 GRAPH
1110 LOCATE 1,7:PRINT "数据中心"," "
1120 PRINT "1.生成多人版数据文件","2.生成单人版数据文件","3.返回主菜单";
1130 K$=INKEY$
1140 IF K$="b" THEN 1180
1150 IF K$="n" THEN 1330
1160 IF K$="m" OR ASC(K$)=27 THEN 50
1170 BEEP:GOTO 1130
1180 GRAPH
1190 LOCATE 3,6:PRINT "写入中 ..."
1200 DIM N$(30):N=1
1210 N$(N)="巡逻艇":N=N+1:N$(N)="驱逐舰":N=N+1:N$(N)="巡洋舰":N=N+1
1220 N$(N)="战列舰":N=N+1:N$(N)="航空母舰":N=N+1:N$(N)="舰载攻击机":N=N+1
1225 N$(N)="导弹类兵器":N=N+1:N$(N)="舰船升级系统":N=N+1:N$(N)="战斗轰炸机":N=N+1
1230 N$(N)="侦察机":N=N+1:N$(N)="超音速侦察机":N=N+1:N$(N)="重型反舰导弹":N=N+1
1240 N$(N)="多弹头导弹":N=N+1:N$(N)="高级多弹头导弹":N=N+1:N$(N)="舰载机导航系统":N=N+1
1250 N$(N)="宙斯盾系统":N=N+1:N$(N)="精确制导系统":N=N+1:N$(N)="<Q>":N=N+1
1260 N$(N)="<A>":N=N+1:N$(N)="<Z>":N=N+1:N$(N)="<W>":N=N+1:N$(N)="<S>":N=N+1:N$(N)="<X>"
1270 N=N+1:N$(N)="                  ":N=N+1:N$(N)="新兵":N=N+1:N$(N)="炮手":N=N+1
1280 N$(N)="舰长":N=N+1:N$(N)="舰队司令":N=N+1:N$(N)="将军":N=N+1:N$(N)="元帅"
1285 OPEN "SeaMPData" FOR OUTPUT AS #1
1290 FOR I=1  TO N
1300 WRITE #1,N$(I)
1310 NEXT I
1320 CLOSE #1:BEEP:GRAPH:LOCATE 3,9:PRINT "Ok!!":K$=INKEY$:GOTO 50
1330 GRAPH
1340 LOCATE 3,6:PRINT "写入中 ..."
1350 DIM N$(51):N=1
1360 N$(N)="指挥官，":N=N+1:N$(N)="我们的":N=N+1:N$(N)="敌人的":N=N+1
1370 N$(N)="航空母舰":N=N+1:N$(N)="战列舰":N=N+1:N$(N)="巡洋舰":N=N+1
1380 N$(N)="驱逐舰":N=N+1:N$(N)="巡逻艇":N=N+1:N$(N)="被击中！":N=N+1
1390 N$(N)="被命中要害！":N=N+1:N$(N)="开始冒烟了！":N=N+1
1400 N$(N)="左舷被炸了个大洞！":N=N+1:N$(N)="正在向右倾斜！":N=N+1
1410 N$(N)="沉没了。":N=N+1:N$(N)="泰坦尼克了。":N=N+1:N$(N)="在和鱼一起游泳。":N=N+1
1420 N$(N)="翘辫子了。":N=N+1:N$(N)="不能动了，正在下沉。":N=N+1
1430 N$(N)="你打偏了！":N=N+1:N$(N)="你要先学会瞄准！":N=N+1
1440 N$(N)="打得好！你杀死了一条…鲸鱼！":N=N+1:N$(N)="你是不是忘记带望远镜了？":N=N+1
1450 N$(N)="别再浪废导弹了！":N=N+1:N$(N)="元帅★★★★★":N=N+1
1460 N$(N)="将军★★★★":N=N+1:N$(N)="舰队司令★★★":N=N+1
1470 N$(N)="舰长★★":N=N+1:N$(N)="炮手☆":N=N+1:N$(N)="新兵〓":N=N+1
1480 N$(N)="一分钟之后你，还有你的舰队就去喂鱼啦！":N=N+1
1490 N$(N)="如果你十次能赢我五次的话那你也是将军啦！":N=N+1
1500 N$(N)="新来的，注意一下海里的鲸鱼，别伤到它们！":N=N+1
1510 N$(N)="如果你连我也打不赢的话那你就回家钓鱼吧！":N=N+1
1520 N$(N)="我一向是先开火，再说理由！":N=N+1
1530 N$(N)="我打得不准，但我不笨哦！":N=N+1
1540 N$(N)="     游戏说明":N=N+1:N$(N)="    游戏双方分别在":N=N+1
1550 N$(N)="10×10的海域中部署":N=N+1:N$(N)="五艘战舰，之后互相":N=N+1
1560 N$(N)="轰炸，先击沉敌方所":N=N+1:N$(N)="有战舰者为胜。":N=N+1
1570 N$(N)="部署时按键说明：":N=N+1:N$(N)="  方向键移动战舰":N=N+1
1580 N$(N)="  <C> 键调整方向":N=N+1:N$(N)="  <输入>键确认":N=N+1
1590 N$(N)="游戏时按键说明：":N=N+1:N$(N)="  方向键移动光标":N=N+1
1600 N$(N)="  <输入>键轰炸":N=N+1:N$(N)="    轰炸后上下键察":N=N+1
1610 N$(N)="看状态报告，<继续>":N=N+1:N$(N)="下一玩家攻击。"
1620 OPEN "SeaData" FOR OUTPUT AS #1
1630 FOR I=1  TO N
1640 WRITE #1,N$(I)
1650 NEXT I
1660 CLOSE #1:BEEP:GRAPH:LOCATE 3,9:PRINT "Ok!!":K$=INKEY$:GOTO 50