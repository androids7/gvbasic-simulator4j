5 GRAPH
10 LOCATE 3,6:PRINT "Loading..."
20 OPEN "NFData" FOR RANDOM AS #1:L=LOF(1):CLOSE #1
30 IF L=0 THEN CLS:PRINT "未找到数据文件！":K$=INKEY$:POKE 199,155
40 CLEAR
50 DIM AK(3,7),A(32),B(32),C(32),D(32),E(32),F(32),G(32),H(32),I(32),J(32),O$(10),R(8,5)
55 DIM U(32)
60 OPEN "NFData" FOR INPUT AS #1
70 FOR I=1  TO 7
80 INPUT #1,NA$(I),UK(I),AK(1,I),AK(2,I),AK(3,I),RA(I),TU(I),SP(I),TL(I),VO(I),AM(I)
90 NEXT I
100 FOR I=1  TO 32
110 INPUT #1,A(I),B(I),C(I),D(I),E(I),F(I),G(I)
120 H(I)=SP(A(I)):J(I)=VO(A(I)):U(I)=TU(A(I))
130 NEXT I
140 FOR I=1  TO 8
150 INPUT #1,O$(I)
160 NEXT I
170 CLOSE #1
180 CLS:PRINT O$(1);:MN(1)=16:MN(2)=16:LN(1)=16:LN(2)=16
190 K$=INKEY$
200 IF K$="b" THEN 240
210 IF K$="n" THEN 2220
220 IF K$="m" OR ASC(K$)=27 THEN POKE 199,155
230 BEEP:GOTO 190
240 CLS:P=1
250 LOCATE 3,7:PRINT O$(2);P:K$=INKEY$:CLS
255 LINE 133,66,157,66:LINE 133,72,157,72
260 LINE 129,0,129,79:BOX 133,2,156,29:LINE 133,54,157,54:LINE 133,60,157,60
265 GOTO 490
270 FOR M=1  TO 5:FOR N=1  TO 8:R(N,M)=0:NEXT N:NEXT M
275 BOX 134,3,154,28,1,0:MX=WX*4+131:MY=WY*3+1:BOX MX,MY,MX+3,MY+2,1:BOX 0,0,127,79,1,0
280 FOR I=1  TO 32
285 IF F(I)<>0 OR A(I)=0 THEN 310
290 IF B(I)=WX AND C(I)=WY THEN R(D(I),E(I))=I:GOTO 295 ELSE 300
295 IF B(I)=B(CV) AND C(I)=C(CV) AND D(I)=D(CV) AND E(I)=E(CV) THEN R(D(I),E(I))=CV
300 IF B(I)=WX AND C(I)=WY THEN Z=I:GOSUB 330
310 NEXT I
320 ON KI GOTO 530,865
330 X=D(Z)*16-7:Y=E(Z)*16-8:UA=INT((Z-0.5)/16)+1:BOX X-8,Y-8,X+7,Y+7,1,0:
335 BOX X-(1.5-UA)*10-1,Y-6,X-(1.5-UA)*10,Y-5
336 LINE X-UA*3,Y-6,X-UA*3+G(Z)/TL(A(Z))*8,Y-6
340 ON A(Z)GOTO 350,370,390,410,430,450,470
350 LINE X-5,Y+4,X+4,Y+4:LINE X-4,Y+5,X+5,Y+5:LINE X-2,Y+3,X-1,Y+3:LINE X+1,Y+2,X+2,Y+3
360 DRAW X-1,Y+2:GOTO 480
370 LINE X-6,Y+4,X+4,Y+4:LINE X-5,Y+5,X+7,Y+5:LINE X-2,Y+3,X+3,Y+3:LINE X-1,Y+2,X+2,Y+2
380 LINE X,Y-1,X,Y+1:DRAW X-1,Y+1:GOTO 480
390 LINE X-6,Y+5,X+7,Y+5:LINE X-5,Y+4,X+3,Y+4:BOX X-3,Y+2,X-2,Y+3:LINE X+6,Y+4,X+7,Y+4
400 DRAW X-2,Y+1:DRAW X+7,Y+3:GOTO 480
410 BOX X-6,Y+4,X+7,Y+5:LINE X-4,Y+3,X+4,Y+3:LINE X+2,Y-1,X+2,Y+2:BOX X-2,Y+1,X,Y+2
420 DRAW X-7,Y+4:DRAW X-1,Y:GOTO 480
430 BOX X-6,Y+3,X+7,Y+5,1:LINE X-4,Y+2,X+5,Y+2:LINE X-2,Y+1,X+3,Y+1:BOX X,Y-1,X+1,Y
440 DRAW X-7,Y+3:DRAW X-6,X+5,0:DRAW X+7,Y+5,0:DRAW X,Y-2:GOTO 480
450 BOX X-7,Y+3,X+7,Y+5,1:BOX X+3,Y+1,X+6,Y+2:LINE X+5,Y-2,X+5,Y:LINE X+2,Y,X+3,Y
460 DRAW X-8,Y+3:DRAW X-7,Y+5,0:DRAW X+7,Y+5,0:GOTO 480
470 LINE X-2,Y,X+3,Y:LINE X-1,Y,X+1,Y-2:LINE X-1,Y,X+1,Y+2:LINE X+3,Y-1,X+3,Y+1
480 RETURN
490 FOR S=P*16-15  TO P*16:V=A(S):CV=P*16-6:IF V=0 OR H(S)+U(S)=0 THEN 1130
500 IF V=7 AND F(S)<>0 THEN 510 ELSE 520
510 B(S)=B(CV):C(S)=C(CV):D(S)=D(CV):E(S)=E(CV):F(S)=0
515 IF WX=B(S) AND WY=C(S) THEN Z=S:GOSUB 330
520 IF B(S)=WX AND C(S)=WY THEN 530 ELSE KI=1:WX=B(S):WY=C(S):GOTO 270
530 UX=D(S)*16-15:UY=E(S)*16-16:BOX UX,UY,UX+15,UY+15,0,2
540 LOCATE 3,18:PRINT NA$(V)
550 L=G(S)/TL(V)*24:BOX 133,52,157,53,1,0:BOX 133,52,133+L,53,1
560 L=H(S)/SP(V)*24:BOX 133,58,157,59,1,0:BOX 133,58,133+L,59,1
570 L=U(S)/TU(V)*24:BOX 133,64,157,65,1,0:BOX 133,64,133+L,65,1
580 BOX 133,70,157,71,1,0
590 IF V=7 THEN 600 ELSE 610
600 L=J(S)/VO(V)*24:BOX 133,70,133+L,71,1
610 K=ASC(INKEY$)
620 IF K=98 AND H(S)<>0 THEN MK=1:GOTO 680
630 IF K=110 AND U(S)<>0 THEN MK=2:GOTO 680
640 IF K=109 THEN 1080
650 IF K=103 THEN 1070
660 IF K=104 THEN 2000
670 BEEP:GOTO 610
680 SX=D(S):SY=E(S):BOX SX*16-15,SY*16-16,SX*16,SY*16-1,1,2
690 K=ASC(INKEY$)
700 IF K=20 THEN TX=SX:TY=SY-1:GOTO 760
710 IF K=21 THEN TX=SX:TY=SY+1:GOTO 760
720 IF K=22 THEN TX=SX+1:TY=SY:GOTO 760
730 IF K=23 THEN TX=SX-1:TY=SY:GOTO 760
735 IF K=98 AND MK=1 THEN BOX SX*16-15,SY*16-16,SX*16,SY*16-1,1,2:GOTO 755
740 IF K=110 AND MK=2 THEN BOX SX*16-15,SY*16-16,SX*16,SY*16-1,1,2:GOTO 755
745 IF K=13 THEN 900
750 BEEP:GOTO 690
755 IF WX=B(S) AND WY=C(S) THEN BOX UX,UY,UX+15,UY+15,0,2
756 GOTO 500
760 DX=ABS((WX-B(S))*8+TX-D(S)):DY=ABS((WY-C(S))*5+TY-E(S))
770 IF (DX>H(S) OR DY>H(S)) AND MK=1 THEN TX=SX:TY=SY:BEEP:GOTO 860
775 IF (DX>RA(V) OR DY>RA(V)) AND MK=2 THEN TX=SX:TY=SY:BEEP:GOTO 860
780 IF TX>8 OR TY>5 OR TX<1 OR TY<1 THEN 790 ELSE 860
790 IF TX>8 AND WX<5 THEN TX=1:WX=WX+1:KI=2:GOTO 270
800 IF TX<1 AND WX>1 THEN TX=8:WX=WX-1:KI=2:GOTO 270
810 IF TY>5 AND WY<8 THEN TY=1:WY=WY+1:KI=2:GOTO 270
820 IF TY<1 AND WY>1 THEN TY=5:WY=WY-1:KI=2:GOTO 270
830 IF (TX<1 AND WX=1) OR (TX>8 AND WX=5) OR (TY<1 AND WY=1) OR (TY>5 AND WY=8) THEN 850 
840 GOTO 860
850 BEEP:TX=SX:TY=SY
860 BOX SX*16-15,SY*16-16,SX*16,SY*16-1,1,2:GOTO 870
865 IF WX=B(S) AND WY=C(S) THEN BOX UX,UY,UX+15,UY+15,0,2
870 SX=TX:SY=TY
880 BOX SX*16-15,SY*16-16,SX*16,SY*16-1,1,2
890 GOTO 690
900 ON MK GOTO 910,1000
910 IF R(SX,SY)=0 OR (R(SX,SY)=CV AND V=7) THEN 920 ELSE 750
920 IF R(SX,SY)=CV AND V=7 THEN F(S)=1:H(S)=0:U(S)=0:GOTO 925 ELSE 935
925 IF DX+DY<>0 AND B(S)=WX AND C(S)=WY THEN R(D(S),E(S))=0:BOX UX,UY,UX+15,UY+15,1,0
930 BOX D(CV)*16-15,E(CV)*16-16,D(CV)*16,E(CV)*16-1:GOTO 1100
935 IF R(D(S),E(S))=CV AND V=7 THEN Z=CV:GOSUB 330:GOTO 950
940 IF B(S)=WX AND C(S)=WY THEN BOX UX,UY,UX+15,UY+15,1,0:R(D(S),E(S))=0
950 R(SX,SY)=S:B(S)=WX:C(S)=WY:D(S)=SX:E(S)=SY:Z=S:GOSUB 330
960 IF DX>=DY THEN DI=DX ELSE DI=DY
965 IF DI>H(S) THEN DI=H(S)
970 H(S)=H(S)-DI:IF V=7 THEN J(S)=J(S)-DI
980 IF J(S)<=0 AND V=7 THEN A(S)=0:LN(P)=LN(P)-1:R(SX,SY)=0:MN(P)=MN(P)-1:GOTO 985 ELSE 990
985 BOX SX*16-15,SY*16-16,SX*16,SY*16-1,1,0:GOTO 1085
990 GOTO 500
1000 TN=R(SX,SY)
1005 IF TN=0 OR AK(UK(A(TN)),V)=0 OR INT((TN-0.5)/16)+1=P THEN 750
1010 U(S)=U(S)-1:BOX SX*16-15,SY*16-16,SX*16,SY*16-1,1,2
1020 G(TN)=G(TN)-AK(UK(A(TN)),V)+AM(A(TN)):UA=INT((TN-0.5)/16)+1
1025 IF G(TN)<=0 AND TN=UA*16-6 THEN G(TN)=0:A(TN)=0:LN(UA)=LN(UA)-1:R(SX,SY)=0:GOTO 1200
1030 IF G(TN)<=0 THEN G(TN)=0:A(TN)=0:LN(UA)=LN(UA)-1:R(SX,SY)=0:GOTO 1035 ELSE 1040
1035 BOX SX*16-15,SY*16-16,SX*16,SY*16-1,1,0:MN(UA)=MN(UA)-1
1040 IF LN(UA)=0 THEN 2060
1045 IF UA=P AND G(S)=0 THEN 1130
1050 IF WX=B(S) AND WY=C(S) THEN BOX UX,UY,UX+15,UY+15,0,2
1055 IF A(TN)<>0 THEN Z=TN:GOSUB 330
1060 GOTO 500
1070 H(S)=0:U(S)=0
1080 BOX UX,UY,UX+15,UY+15,0,2
1085 IF LN(P)=0 THEN 2060
1090 IF V=7 AND R(D(S),E(S))=CV THEN F(S)=1:GOTO 1100 ELSE 1110
1100 H(S)=0:U(S)=0:Z=CV:GOSUB 330
1110 IF H(S)+U(S)=0 THEN MN(P)=MN(P)-1
1120 IF MN(P)<=0 THEN 1150
1130 NEXT S
1140 IF MN(P)<=0 THEN 1150 ELSE 490
1150 FOR I=1  TO 32
1160 H(I)=SP(A(I)):U(I)=TU(A(I))
1165 IF A(I)<>0 AND AM(A(I))<>0 AND G(I)+AM(A(I))<=TL(A(I)) THEN G(I)=G(I)+AM(A(I))
1170 IF F(I)=1 THEN G(I)=TL(A(I)):J(I)=VO(A(I))
1180 NEXT I
1190 MN(1)=LN(1):MN(2)=LN(2):P=3-P:WX=0:WY=0:CLS:GOTO 250
1200 BOX SX*16-15,SY*16-16,SX*16,SY*16-1,1,0:MN(UA)=MN(UA)-1
1210 FOR I=UA*16-5  TO UA*16
1220 IF F(I)=1 THEN A(I)=0:LN(UA)=LN(UA)-1:MN(UA)=MN(UA)-1
1230 NEXT I
1240 GOTO 1040
2000 CLS:PRINT O$(3)
2010 K$=INKEY$
2020 IF K$="b" THEN 2100
2030 IF K$="n" THEN CLS:WX=0:WY=0:GOTO 250
2040 IF K$="m" THEN GOTO 5
2050 BEEP:GOTO 2010
2060 IF LN(1)=0 THEN P=2
2070 IF LN(2)=0 THEN P=1
2080 CLS
2090 PRINT O$(2);P;O$(4):K$=INKEY$:GOTO 5
2100 CLS
2110 OPEN "NFSave" FOR RANDOM AS #1:L=LOF(1):CLOSE #1
2120 IF L<>0 THEN 2130 ELSE 2150
2130 PRINT O$(6):K$=INKEY$
2140 IF K$="n" THEN 2000
2150 CLS:LOCATE 2,1:PRINT O$(5)
2160 OPEN "NFSave" FOR OUTPUT AS #1
2170 WRITE #1,P,MN(1),MN(2),LN(1),LN(2)
2180 FOR I=1  TO 32
2190 WRITE #1,A(I),B(I),C(I),D(I),E(I),F(I),G(I),H(I),J(I),U(I)
2200 NEXT I
2210 CLOSE #1:GOTO 2000
2220 CLS
2230 OPEN "NFSave" FOR RANDOM AS #1:L=LOF(1):CLOSE #1
2240 IF L=0 THEN PRINT O$(7):K=INKEY$:GOTO 
2250 CLS:LOCATE 2,1:PRINT O$(8)
2260 OPEN "NFSave" FOR INPUT AS #1
2270 INPUT #1,P,MN(1),MN(2),LN(1),LN(2)
2280 FOR I=1  TO 32
2290 INPUT #1,A(I),B(I),C(I),D(I),E(I),F(I),G(I),H(I),J(I),U(I)
2300 NEXT I
2310 CLOSE #1
2320 CLS:WX=0:WY=0:GOTO 250